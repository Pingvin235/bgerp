task resetProperties {
    doFirst {
        println "Reset update.properties"
        "git checkout -- $updateProperties".execute()
        println "Reset update_lib.properties"
        "git checkout -- $updateLibProperties".execute()
    }
}

task updateProperties(type: JavaExec, dependsOn: 'compileToolJava', description: 'Updates update.properties file for update module') {
    mustRunAfter 'resetProperties'
    classpath =  sourceSets.tool.runtimeClasspath
    mainClass = 'org.bgerp.tool.prop.Update'
    args version, updateDir, "${->getChangeId()}"
}

task updateLibProperties(type: JavaExec, dependsOn: 'compileToolJava', description: 'Updates update.properties file for update_lib module if exists file build/changes.lib.txt') {
    onlyIf {
        file("build/changes.lib.txt").exists()
    }
    mustRunAfter 'resetProperties'
    classpath =  sourceSets.tool.runtimeClasspath
    mainClass = 'org.bgerp.tool.prop.Update'
    args version, updateLibDir, ''
}

// update_$version_$build.zip has placed to $buildDir/$distsDirName
task buildUpdate(type: Zip, dependsOn: 'buildAppJar', description: 'Builds distribution and update archives') {
    mustRunAfter 'updateProperties'
    doFirst {
        def module = new org.bgerp.tool.prop.Module(updateDir);
        archiveFileName.set(module.zipName());
    }
    from(updateDir) {
        include('db_init.sql')
        include('module.properties')
    }
    into('content') {
        from(distDir) {
            // TODO: Check if needed in update.
            include('scripts/**')
        }
        from("$distDir/files") {
            exclude('bgerp.properties')
            exclude('setenv.sh')
        }
        from(projectDir) {
            // TODO: Check, which files are needed.
            include('docpattern/**')
            include('webapps/**')
        }
    }
    into('content/lib/app') {
        from("$buildDir/$libsDirName").include(appJarName)
    }
}

// bgerp.jar has placed to $buildDir/$libsDirName
task buildAppJar(type: Jar, dependsOn: 'classes') {
    into(versionPackage) {
        def module = new org.bgerp.tool.prop.Module(updateDir);
        from(updateDir) {
            include(module.propertiesName())
        }
    }
    from(sourceSets.main.output) {
        exclude 'log4j.properties'
    }
    archiveFileName = appJarName
}

// update_lib_$version_$build.zip has placed to $buildDir/$distsDirName
task buildUpdateLib(type: Zip, dependsOn: 'buildLibJar', description: 'Builds library update archive if exists file build/changes.lib.txt') {
    mustRunAfter 'updateLibProperties'
    onlyIf {
        file("build/changes.lib.txt").exists()
    }
    doFirst {
        def module = new org.bgerp.tool.prop.Module(updateLibDir);
        archiveFileName.set(module.zipName());
    }
    from(updateLibDir) {
        include('module.properties')
    }
    into('content/lib/app') {
        from("$buildDir/$libsDirName").include(libJarName)
    }
    into('content/lib/ext') {
        from(configurations.runtimeClasspath)
    }
}

// lib.jar has placed to $buildDir/$libsDirName
task buildLibJar(type: Jar) {
    into(versionPackage) {
        def module = new org.bgerp.tool.prop.Module(updateLibDir);
        from(updateLibDir) {
            include(module.propertiesName())
        }
    }
    archiveFileName = libJarName
}

task buildDist(type: Zip, dependsOn: ['buildAppJar', 'buildLibJar'], description: 'Builds distribution archive for the first installation') {
    mustRunAfter 'buildUpdate'
    mustRunAfter 'buildUpdateLib'
    doFirst {
        def module = new org.bgerp.tool.prop.Module(updateDir);
        archiveFileName.set(module.zipName().replace('update_', 'bgerp_'));

        def buildDir = new File(distBuildDir);
        new File(buildDir, 'filestorage').mkdirs();
        new File(buildDir, 'log').mkdirs();

        def toConcatenate = files(new File(updateDir, 'db_init.sql'), new File(distDir, 'db_init.sql'));
        def output = new File(buildDir, 'db_init.sql')
        toConcatenate.each { f -> output << f.text }
    }
    into('bgerp') {
        from(distBuildDir)
        from(distDir) {
            include('scripts/**')
            include('db_create.sql')
        }
        from("$distDir/files")
        from(projectDir) {
            // TODO: Check, which files are needed.
            include('docpattern/**')
            include('webapps/**')
        }
    }
    into('bgerp/lib/app') {
        from("$buildDir/$libsDirName") {
            include(appJarName)
            include(libJarName)
        }
    }
    into('bgerp/lib/ext') {
        from(configurations.runtimeClasspath)
    }
}