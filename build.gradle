plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    // id "org.sonarqube" version "3.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        // com.lowagie:itext:2.1.7.js6
        url "http://repo.bgerp.org/repository/maven-public"
        allowInsecureProtocol = true
        metadataSources { artifact() }
    }
}

sourceCompatibility = '11'
targetCompatibility = '11'

// Eclipse and VS Code use bin
buildDir = 'target'

version = '3.0'

eclipse {
    classpath {
        // speed up of initial start, change if needed
        downloadJavadoc = false
        downloadSources = false
    }
}

/* sonarqube {
    properties {
        property "sonar.projectKey", "org.bgerp"
        property "sonar.organization", "bgerp"
        property "sonar.host.url", "https://sonarcloud.io"
    }
} */

idea {
    module {}
}

configurations {
    all {
        transitive = false
    }
    doc {
        transitive = true
    }
    itest.extendsFrom(implementation)
    tools.extendsFrom(implementation)
    // to be compiled inside IDE
    compileClasspath.extendsFrom(implementation, tools, itest)
}

dependencies {
    // OpenJDK 11
    // http://qaru.site/questions/16251978/migration-jaxws-application-from-java-8-to-java-11
    // https://www.jesperdj.com/2018/09/30/jaxb-on-java-9-10-11-and-beyond/
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1' // 2.4.0-b180830.0438
    implementation 'javax.xml.ws:jaxws-api:2.3.1'
    implementation 'javax.jws:javax.jws-api:1.1'
    implementation 'com.sun.xml.ws:jaxws-rt:2.3.1'
    implementation 'com.sun.xml.ws:rt:2.3.1'
    implementation 'com.sun.istack:istack-commons-runtime:3.0.7'
    implementation 'com.sun.xml.stream.buffer:streambuffer:1.5.6'
    implementation 'com.sun.xml.ws:policy:2.7.5'
    implementation 'org.jvnet.staxex:stax-ex:1.8'
    implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
    implementation 'com.sun.xml.messaging.saaj:saaj-impl:1.5.0'
    implementation 'org.glassfish.gmbal:gmbal-api-only:3.1.0-b001'
    implementation 'org.jvnet.mimepull:mimepull:1.9.10'

    // библиотеки для BGERP, утилит и плагинов (добавлять после новые библиотеки)
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.apache.logging.log4j:log4j-api:2.11.2'

    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.50'
    implementation 'org.eclipse.jdt:ecj:3.26.0'
    implementation 'org.apache.tomcat.embed:tomcat-embed-el:9.0.50'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.51.bgerp'
    implementation 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.2'
    implementation 'org.apache.taglibs:taglibs-standard-impl:1.2.5'

    implementation 'org.apache.struts:struts-core:1.3.10'
    implementation 'org.apache.struts:struts-extras:1.3.10'
    implementation 'org.apache.struts:struts-taglib:1.3.10'
    implementation 'org.apache.struts:struts-tiles:1.3.10'

    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'javax.activation:activation:1.1.1' //TODO: Fix using classes and remove library.
    implementation 'org.apache.james:apache-mime4j-dom:0.8.5.bgerp'
    implementation 'org.apache.james:apache-mime4j-core:0.8.5.bgerp'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'commons-beanutils:commons-beanutils:1.9.3'
    implementation 'commons-chain:commons-chain:1.2'
    implementation 'commons-collections:commons-collections:3.2.2'  //bgerp 'org.apache.commons:commons-collections4:4.2'
    implementation 'commons-dbcp:commons-dbcp:1.4'
    implementation 'commons-digester:commons-digester:2.1' // bgerp 'org.apache.commons:commons-digester3:3.2'
    implementation 'commons-el:commons-el:1.0'
    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-jexl3:3.1'
    implementation 'commons-lang:commons-lang:2.6'  // implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'commons-logging:commons-logging:1.2'
    implementation 'commons-pool:commons-pool:1.6' // bgerp 'org.apache.commons:commons-pool2:2.6.0'
    implementation 'commons-validator:commons-validator:1.6'
    implementation 'ar.com.fdvs:DynamicJasper:5.1.1'
    implementation 'org.apache.httpcomponents:fluent-hc:4.5.7'
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'
    implementation 'org.apache.httpcomponents:httpcore:4.4.11'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.1.3'
    implementation 'net.sf.jasperreports:jasperreports:6.7.0'
    runtimeOnly 'com.lowagie:itext:2.1.7.js6'
    implementation 'org.jsoup:jsoup:1.11.3' // TODO: update
    implementation 'mysql:mysql-connector-java:8.0.19'
    implementation 'org.apache.poi:poi:4.0.1'
    runtimeOnly 'org.apache.commons:commons-collections4:4.3' // required for poi
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.itextpdf:itextpdf:5.5.13'

    implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.javassist:javassist:3.27.0-GA'

    // Asterisk
    implementation 'org.asteriskjava:asterisk-java:3.3.0'
    // dep 'org.reflections:reflections'
    // dep 'org.javassist:javassist'

    // License check
    implementation 'org.bouncycastle:bcprov-jdk15on:1.68'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'
    implementation 'com.hierynomus:sshj:0.30.0'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    runtimeOnly 'org.slf4j:slf4j-api:1.7.30'
    runtimeOnly 'org.slf4j:slf4j-log4j12:1.7.30'

    // Telegram
    implementation 'org.telegram:telegrambots:5.3.0'
    implementation 'org.telegram:telegrambots-meta:5.3.0'
    implementation 'aopalliance:aopalliance:1.0'
    implementation 'org.json:json:20180813'
    implementation 'com.google.guava:failureaccess:1.0.1'
    implementation 'com.google.inject:guice:4.2.2'
    implementation 'javax.inject:javax.inject:1'

    // библиотеки, сохранённые в проекте (старый подход, больше не добавлять)
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    // documentation library
    doc 'org.pzdcdoc:pzdcdoc:2.6'

    // unit tests of the main code
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'

    // integration test
    itest 'org.testng:testng:7.4.0'
    itest 'com.beust:jcommander:1.81'
    itest 'org.seleniumhq.selenium:selenium-java:3.141.59'
    itest 'org.seleniumhq.selenium:selenium-api:3.141.59'
    itest 'org.seleniumhq.selenium:selenium-remote-driver:3.141.59'
    itest 'org.seleniumhq.selenium:selenium-support:3.141.59'
    itest 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    itest 'com.squareup.okhttp3:okhttp:3.13.1'
    itest 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'
    itest 'com.squareup.okio:okio:2.2.2'
    itest 'net.bytebuddy:byte-buddy:1.9.10'
    itest 'org.apache.commons:commons-exec:1.3'
}

// called from ant build/update_lib/build.xml
task copyLibExt(type: Copy) {
    into "$buildDir/lib"
    from configurations.runtimeClasspath
}

sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'dyn'
            srcDir 'custom/src'
        }
        resources {
            srcDir 'src'
            srcDir 'custom/src'
        }
        // makes warning on assigning simple output.classesDir
        output.resourcesDir = "${buildDir}/classes/java/main"
    }
    test {
        java {
            srcDir 'srcx/test'
        }
        resources {
            srcDir 'srcx/test'
        }
    }
    tools {
        java {
            srcDir 'srcx/tools'
        }
        compileClasspath = configurations.tools + sourceSets.main.output
        runtimeClasspath = compileClasspath + output
    }
    itest {
        java {
            srcDir 'srcx/itest'
        }
        resources {
            srcDir 'srcx/itest'
        }
        // makes warning on assigning simple output.classesDir
        output.resourcesDir = "${buildDir}/classes/java/itest"
        compileClasspath = configurations.itest + sourceSets.main.output
        runtimeClasspath = compileClasspath + output + configurations.runtimeClasspath
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
    jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
}

task doc(type: JavaExec, description: 'Build manual') {
    classpath =  configurations.doc
    mainClass = 'org.pzdcdoc.Generator'
    args '--in', 'srcx/doc', '--out', 'target/doc'
}

task buildDoc(dependsOn: 'doc', description: 'Backward compatible name for doc') {
    doFirst {
        logger.warn("Use 'doc' task")
    }
}

task buildJavaDoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    failOnError = false
    classpath = configurations.runtimeClasspath
    source = sourceSets.main.allJava
    destinationDir = file("${buildDir}/javadoc")
}

task patchChanges(type: JavaExec, dependsOn: 'compileToolsJava', description: 'Concatenates build/changes.*.txt files to build/changes.txt') {
    classpath =  sourceSets.tools.runtimeClasspath
    mainClass = 'ru.bgerp.tool.PatchChanges'
    args "${->getBuild()}", 'build/changes.txt'
}

def getBuild() {
    def buildProperties = new Properties()
    file("$projectDir/build/update/update.properties").withInputStream { stream -> buildProperties.load(stream) }
    def build = buildProperties.get('build.number')
    println "Build: $build"
    return build
}

task rss(type: JavaExec, dependsOn: 'compileToolsJava', description: 'Generates build/changes.rss out of build/changes.txt') {
    mustRunAfter 'patchChanges'
    classpath =  sourceSets.tools.runtimeClasspath
    mainClass = 'ru.bgerp.tool.RssGenerator'
    args version, 'build/changes.txt', 'build/changes.xml'
}

task buildDist(type: Exec, dependsOn: 'classes', description: 'Builds distribution archive') {
    workingDir "$projectDir/build/bgerp"
    commandLine "ant"
    environment classesPath: "../../target/classes/java/main", changeId: "${->getChangeId()}"
}

// for some reasons the task is named as 'classes' in 'dependsOn'
compileJava {
    doFirst {
        def custom = file("custom");
        if (custom.exists()) {
            custom.renameTo("_custom");
            logger.info("Renaming directory 'custom' to '_custom'");
        }
    }
}

task buildUpdate(type: Exec, dependsOn: 'classes', description: 'Builds distribution and update archives') {
    workingDir "$projectDir/build/update"
    commandLine "ant"
    environment classesPath: "../../target/classes/java/main", changeId: "${->getChangeId()}"
}

task buildUpdateLib(type: Exec, description: 'Builds library update archive if exists file build/changes.lib.txt') {
    onlyIf {
        file("build/changes.lib.txt").exists()
    }
    workingDir "$projectDir/build/update_lib"
    commandLine "ant"
}

task buildUpdateLibForce(type: Exec, description: 'Builds library update archive') {
    workingDir "$projectDir/build/update_lib"
    commandLine "ant"
}

task buildClean(type: Delete) {
    delete fileTree("$projectDir/build").matching {
        include "bgerp/bgerp_${version}_*.zip"
        include "update/update_${version}_*.zip"
        include "update_lib/update_lib_${version}_*.zip"
    }
}

task resetProperties {
    doFirst {
        println "Reset update.properties"
        "git checkout -- $projectDir/build/update/update.properties".execute()
        println "Reset update_lib.properties"
        "git checkout -- $projectDir/build/update_lib/update_lib.properties".execute()
    }
}

task touchChanges(description: "Creates file 'build/changes.<PROCESS_ID>.txt' if missing") {
    doFirst {
        def processId = getChangeIdOrThrow()
        def changesFile = file("$projectDir/build/changes.${processId}.txt")
        if (!changesFile.exists())
            changesFile.text = ""
    }
}

task touchChangesLib(description: "Creates file 'build/changes.lib.txt' if missing") {
    doFirst {
        def changesLibFile = file("$projectDir/build/changes.lib.txt")
        if (!changesLibFile.exists())
            changesLibFile.text = ""
    }
}

task checkChanges(description: 'Checks existance of build/changes.<PROCESS_ID>.txt file') {
    doFirst {
        def processId = getChangeIdOrThrow()
        def changesFileName = "build/changes.${processId}.txt"
        def changesFile = file("$projectDir/$changesFileName")
        if (!changesFile.exists() || changesFile.text.isBlank())
            throw new GradleException("Changes file '$changesFileName' not found or blank. Create it using './gradlew touchChanges' and fill out.")
    }
}

def getChangesFileName() {
    def processId = getChangeIdOrThrow()
    def changesFile = file("$projectDir/build/changes.${processId}.txt")
}

def getBranchName() {
    def branchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    println "Branch name: $branchName"
    return branchName
}

/*
    Gets numeric change ID 'NNNNN' from GIT branch name like: 'pNNNNN-some-description'.
    Throws an exception in case of another branch name.
*/
def getChangeIdOrThrow() {
    def branchName = getBranchName()

    def m  = branchName =~ /^p(\d+)\-/
    if (!m) throw new GradleException("Incorrect branch name: $branchName")

    def changeId = m[0][1]
    println "Change ID: $changeId"

    return changeId
}

/*
    Gets numeric change ID 'NNNNN' from GIT branch name like: 'pNNNNN-some-description'.
    Returns empty string in case of another branch name.
*/
def getChangeId() {
    def branchName = getBranchName()

    def m  = branchName =~ /^p(\d+)\-/
    if (!m) return "";

    def changeId = m[0][1]
    println "Change ID: $changeId"

    return changeId
}

apply from: "build/publish.gradle"

task startServer(type: JavaExec, dependsOn: 'classes', description: 'Starts server with bgerp.properties') {
    classpath =  sourceSets.main.runtimeClasspath
    mainClass = 'org.bgerp.Server'
    args 'start'
    systemProperties = [
        'bgerp.setup.data': findProperty('bgerp.setup.data') ?: 'bgerp',
        'java.net.preferIPv4Stack': true
    ]
}

task licSign(type: JavaExec, dependsOn: 'classes', description: 'Sign a license file [Deprecated]') {
    classpath =  sourceSets.main.runtimeClasspath
    mainClass = 'ru.bgcrm.plugin.License'
    args"${->findProperty('lic.path')}", "${->findProperty('signer.id')}",
        "${->findProperty('key.path')}", "${->findProperty('key.pswd')}"
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = "full"
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen {false}
}

/*
    Complete run:
        gradlew itest -Pdb.user=* -Pdb.pswd=* -Pdb.host=localhost
    Only wanted groups with dependencies:
        -Pgroups.include=initUsers
    Skip heavy DB reset:
        -Pskip.dbReset=true
*/
task itest(type: Test, description: 'Integration tests') {
    // failFast = true SQL logs are not visible

    useTestNG() {
        def propInclude = findProperty("groups.include")
        if (propInclude) {
            includeGroups propInclude
        }
    }
    classpath = sourceSets.itest.runtimeClasspath
    testClassesDirs = sourceSets.itest.output.classesDirs
    testLogging {
        showStandardStreams = true
        exceptionFormat = "full"
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen {false}

    systemProperty "db.host", findProperty("db.host")
    systemProperty "db.user", findProperty("db.user")
    systemProperty "db.pswd", findProperty("db.pswd")
    systemProperty "webdriver.chrome.driver", findProperty("webdriver.chrome.driver")
    systemProperty "skip.dbReset", findProperty("skip.dbReset")
}

task integrationTest(dependsOn: 'itest', description: 'Backward compatible name for itest') {
    doFirst {
        logger.warn("Use 'itest' task")
    }
}
