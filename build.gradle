plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    // id "org.sonarqube" version "3.3"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://repo.bgerp.org/repository/maven-public"
        metadataSources { artifact() }
    }
}

sourceCompatibility = '17'
targetCompatibility = '17'

// Eclipse and VS Code use bin
buildDir = 'target'

version = '3.0'

project.ext {
    updateDir="$projectDir/build/update"
    updateProperties="$updateDir/update.properties"
    updateLibDir="$projectDir/build/update_lib"
    updateLibProperties="$updateLibDir/update_lib.properties"
    distDir="$projectDir/build/bgerp"
    classesPath="$buildDir/classes/java/main"
    versionPackage='ru/bgcrm/version'
    appJarName='bgerp.jar'
    libJarName='lib.jar'
    distBuildDir="$buildDir/bgerp"
}

eclipse {
    classpath {
        // speed up of initial start, change if needed
        downloadJavadoc = false
        downloadSources = false
    }
}

/* sonarqube {
    properties {
        property "sonar.projectKey", "org.bgerp"
        property "sonar.organization", "bgerp"
        property "sonar.host.url", "https://sonarcloud.io"
    }
} */

idea {
    module {}
}

configurations {
    all {
        transitive = false
    }
    doc {
        transitive = true
    }
    itest.extendsFrom(implementation)
    tool.extendsFrom(implementation)
    // to be compiled inside IDE
    compileClasspath.extendsFrom(implementation, tool, itest)
}

dependencies {
    // OpenJDK 11
    // http://qaru.site/questions/16251978/migration-jaxws-application-from-java-8-to-java-11
    // https://www.jesperdj.com/2018/09/30/jaxb-on-java-9-10-11-and-beyond/
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:2.3.1' // 2.4.0-b180830.0438
    implementation 'javax.xml.ws:jaxws-api:2.3.1'
    implementation 'javax.jws:javax.jws-api:1.1'
    runtimeOnly 'com.sun.xml.ws:jaxws-rt:2.3.1'
    runtimeOnly 'com.sun.xml.ws:rt:2.3.1'
    runtimeOnly 'com.sun.istack:istack-commons-runtime:3.0.7'
    runtimeOnly 'com.sun.xml.stream.buffer:streambuffer:1.5.6'
    runtimeOnly 'com.sun.xml.ws:policy:2.7.5'
    runtimeOnly 'org.jvnet.staxex:stax-ex:1.8'
    implementation 'javax.xml.soap:javax.xml.soap-api:1.4.0'
    runtimeOnly 'com.sun.xml.messaging.saaj:saaj-impl:1.5.0'
    runtimeOnly 'org.glassfish.gmbal:gmbal-api-only:3.1.0-b001'
    runtimeOnly 'org.jvnet.mimepull:mimepull:1.9.10'

    // add app libraries below with explicitly required dependencies after each of them
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.apache.logging.log4j:log4j-api:2.11.2'

    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:9.0.50'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:9.0.51.bgerp' // also contains Servlet API
    runtimeOnly 'org.apache.tomcat.embed:tomcat-embed-el:9.0.50'
    runtimeOnly 'org.eclipse.jdt:ecj:3.26.0'
    runtimeOnly 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.servlet.jsp.jstl:javax.servlet.jsp.jstl-api:1.2.2' // there are extended tags in the project
    implementation 'org.apache.taglibs:taglibs-standard-impl:1.2.5' // there are extended tags in the project

    implementation 'org.apache.struts:struts-core:1.3.10'
    implementation 'org.apache.struts:struts-extras:1.3.10'
    implementation 'org.apache.struts:struts-taglib:1.3.10'
    runtimeOnly 'commons-chain:commons-chain:1.2'
    runtimeOnly 'commons-digester:commons-digester:2.1'

    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'javax.activation:activation:1.1.1' //TODO: Fix using classes and remove library.
    implementation 'org.apache.james:apache-mime4j-dom:0.8.5.bgerp'
    implementation 'org.apache.james:apache-mime4j-core:0.8.5.bgerp'

    implementation 'commons-beanutils:commons-beanutils:1.9.3' // DynActionForm
    implementation 'commons-collections:commons-collections:3.2.2' // 'org.apache.commons:commons-collections4' has a different package

    implementation 'commons-dbcp:commons-dbcp:1.4' // migrate to 'commons-dbcp2'
    implementation 'commons-pool:commons-pool:1.6' // migrate to 'commons-pool2'
    runtimeOnly 'mysql:mysql-connector-java:8.0.19'

    implementation 'commons-fileupload:commons-fileupload:1.4'
    implementation 'commons-io:commons-io:2.11.0'

    implementation 'org.apache.commons:commons-jexl3:3.2.1'
    runtimeOnly 'commons-logging:commons-logging:1.2'

    implementation 'org.apache.httpcomponents:fluent-hc:4.5.7'
    implementation 'org.apache.httpcomponents:httpclient:4.5.7'
    implementation 'org.apache.httpcomponents:httpcore:4.4.11'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

    implementation 'net.sf.jasperreports:jasperreports:6.7.0'
    runtimeOnly 'com.lowagie:itext:2.1.7.js6'
    implementation 'ar.com.fdvs:DynamicJasper:5.1.1'
    implementation 'org.apache.poi:poi:4.0.1' // directly used in Callboard plugin, for making Russian work days table
    runtimeOnly 'org.apache.commons:commons-collections4:4.3'

    // Asterisk
    implementation 'org.asteriskjava:asterisk-java:3.37.0'
    // dep 'org.reflections:reflections'
    // dep 'org.javassist:javassist'

    // License check
    implementation 'org.bouncycastle:bcprov-jdk15on:1.68'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'
    implementation 'com.hierynomus:sshj:0.30.0'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    runtimeOnly 'org.slf4j:slf4j-api:1.7.30'
    runtimeOnly 'org.slf4j:slf4j-log4j12:1.7.30'

    // Telegram
    implementation 'org.telegram:telegrambots:6.3.0'
    implementation 'org.telegram:telegrambots-meta:6.3.0'
    // implementation 'aopalliance:aopalliance:1.0'
    // implementation 'com.google.guava:failureaccess:1.0.1'
    // implementation 'com.google.inject:guice:4.2.2'
    // implementation 'javax.inject:javax.inject:1'

    // separated libraries without transitive dependencies should be added the end of the list
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.javassist:javassist:3.29.2-GA'
    implementation 'com.itextpdf:itextpdf:5.5.13' // used in Document plugin for filling out PDF forms
    implementation 'org.json:json:20220924'

    // implementation 'commons-el:commons-el:1.0'
    // implementation 'commons-validator:commons-validator:1.6'

    // project stored libraries, do not use anymore
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    // documentation library
    doc 'org.pzdcdoc:pzdcdoc:2.6'

    // tool libraries
    tool 'org.apache.ant:ant:1.10.12'

    // unit tests of the main code
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'

    // integration test
    itest 'org.testng:testng:7.7.1'
    itest 'com.beust:jcommander:1.82'
    // Selenium related libs.
    // itest 'org.seleniumhq.selenium:selenium-java:3.141.59'
    // itest 'org.seleniumhq.selenium:selenium-api:3.141.59'
    // itest 'org.seleniumhq.selenium:selenium-remote-driver:3.141.59'
    // itest 'org.seleniumhq.selenium:selenium-support:3.141.59'
    // itest 'org.seleniumhq.selenium:selenium-chrome-driver:3.141.59'
    // itest 'com.squareup.okhttp3:okhttp:3.13.1'
    // itest 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'
    // itest 'com.squareup.okio:okio:2.2.2'
    // itest 'net.bytebuddy:byte-buddy:1.9.10'
    // itest 'org.apache.commons:commons-exec:1.3'
}

sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'dyn'
            srcDir 'custom/src'
        }
        resources {
            srcDir 'src'
            srcDir 'custom/src'
        }
        // makes warning on assigning simple output.classesDir
        output.resourcesDir = "$buildDir/classes/java/main"
    }
    test {
        java {
            srcDir 'srcx/test'
        }
        resources {
            srcDir 'srcx/test'
        }
    }
    tool {
        java {
            srcDir 'buildSrc/src/main/java'
            srcDir 'srcx/tool'
        }
        compileClasspath = configurations.tool + sourceSets.main.output
        runtimeClasspath = compileClasspath + output
    }
    itest {
        java {
            srcDir 'srcx/itest'
        }
        resources {
            srcDir 'srcx/itest'
        }
        // makes warning on assigning simple output.classesDir
        output.resourcesDir = "$buildDir/classes/java/itest"
        compileClasspath = configurations.itest + sourceSets.main.output
        runtimeClasspath = compileClasspath + output + configurations.runtimeClasspath
    }
}

// supporting functions, not found if be extracted to a separated .gradle file

def getBranchName() {
    def branchName = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
    println "Branch name: $branchName"
    return branchName
}

def getBuild() {
    def buildProperties = new Properties()
    file("$updateProperties").withInputStream { stream -> buildProperties.load(stream) }
    def build = buildProperties.get('build.number')
    println "Build: $build"
    return build
}

/*
    Gets numeric change ID 'NNNNN' from GIT branch name like: 'pNNNNN-some-description'.
    Returns empty string in case of another branch name.
*/
def getChangeId() {
    def branchName = getBranchName()

    def m  = branchName =~ /^p(\d+)\-/
    if (!m) return "";

    def changeId = m[0][1]
    println "Change ID: $changeId"

    return changeId
}

/*
    Gets numeric change ID 'NNNNN' from GIT branch name like: 'pNNNNN-some-description'.
    Throws an exception in case of another branch name.
*/
def getChangeIdOrThrow() {
    def branchName = getBranchName()

    def m  = branchName =~ /^p(\d+)\-/
    if (!m) throw new GradleException("Incorrect branch name: $branchName")

    def changeId = m[0][1]
    println "Change ID: $changeId"

    return changeId
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
    jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
}

// 'compileJava' is executed prior to 'classes' from 'dependsOn'
compileJava {
    doFirst {
        println "Checking existance of 'custom' directory"
        def custom = file("custom");
        if (custom.exists()) {
            custom.renameTo("_custom");
            println "Renaming directory 'custom' to '_custom'"
        }
    }
}

task doc(type: JavaExec, description: 'Builds manual') {
    classpath =  configurations.doc
    mainClass = 'org.pzdcdoc.Generator'
    args '--in', 'srcx/doc', '--out', "$buildDir/doc"
}

task jdoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    failOnError = false
    classpath = configurations.runtimeClasspath
    source = sourceSets.main.allJava
    destinationDir = file("$buildDir/javadoc")
}

task touchChanges(description: "Creates file 'build/changes.<PROCESS_ID>.txt' if missing") {
    doFirst {
        def processId = getChangeIdOrThrow()
        def changesFile = file("$projectDir/build/changes.${processId}.txt")
        if (!changesFile.exists())
            changesFile.text = ""
    }
}

task touchChangesLib(description: "Creates file 'build/changes.lib.txt' if missing") {
    doFirst {
        def changesLibFile = file("$projectDir/build/changes.lib.txt")
        if (!changesLibFile.exists())
            changesLibFile.text = ""
    }
}

task checkChanges(description: 'Checks existance of build/changes.<PROCESS_ID>.txt file') {
    doFirst {
        def processId = getChangeIdOrThrow()
        def changesFileName = "build/changes.${processId}.txt"
        def changesFile = file("$projectDir/$changesFileName")
        if (!changesFile.exists() || changesFile.text.isBlank())
            throw new GradleException("Changes file '$changesFileName' not found or blank. Create it using './gradlew touchChanges' and fill out.")
    }
}

apply from: "build/build.gradle"

task changesFile(type: JavaExec, dependsOn: 'compileToolJava', description: 'Concatenates build/changes.*.txt files to build/changes.txt') {
    classpath =  sourceSets.tool.runtimeClasspath
    mainClass = 'org.bgerp.tool.ChangesFile'
    args "${->getBuild()}", 'build/changes.txt'
}

task changesRss(type: JavaExec, dependsOn: 'compileToolJava', description: 'Generates build/changes.rss out of build/changes.txt') {
    mustRunAfter 'changesFile'
    classpath =  sourceSets.tool.runtimeClasspath
    mainClass = 'org.bgerp.tool.ChangesRss'
    args version, 'build/changes.txt', 'build/changes.xml'
}

task changesDoc(type: JavaExec, description: "Adjusts changes link for the current build in documentation files") {
    classpath = sourceSets.tool.runtimeClasspath
    mainClass = 'org.bgerp.tool.ChangesDoc'
    args "${->getBuild()}", 'srcx/doc'
}

apply from: "build/publish.gradle"

task startServer(type: JavaExec, dependsOn: 'classes', description: 'Starts server with bgerp.properties') {
    classpath =  sourceSets.main.runtimeClasspath
    mainClass = 'org.bgerp.Server'
    args 'start'
    systemProperties = [
        'bgerp.setup.data': findProperty('bgerp.setup.data') ?: 'bgerp',
        'java.net.preferIPv4Stack': true
    ]
}

apply from: "test.gradle"
